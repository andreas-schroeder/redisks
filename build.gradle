buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath 'cz.alenkacz:gradle-scalafmt:1.5.0'
    }
}


plugins {
    id 'com.github.maiflai.scalatest' version '0.32'
    id "org.scoverage" version '7.0.1'
}

allprojects {
    apply plugin: 'scala'
    apply plugin: 'scalafmt'
    apply plugin: 'idea'
    apply plugin: 'com.github.maiflai.scalatest'
    apply plugin: 'maven-publish'

    sourceCompatibility = '1.8'
    group = artifactGroup
    version = artifactVersion

    sourceSets {
        main {
            scala {
                // build order is important
                srcDirs = ['src/main/scala', 'src/main/java']
            }
            java {
                srcDirs = []
            }
        }
    }

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencies {

        implementation       group: 'org.scala-lang',             name: 'scala-library', version: '2.12.15'
        implementation       group: 'org.apache.kafka',           name: 'kafka-streams', version: kafkaVersion
        implementation       group: 'biz.paluch.redis',           name: 'lettuce',       version: '4.4.0.Final'
        implementation       group: 'org.jetbrains',              name: 'annotations',   version: '23.0.0'

        implementation scala(group: 'io.reactivex',               name: 'rxscala',       version: '0.27.0')
        implementation scala(group: 'com.typesafe.scala-logging', name: 'scala-logging', version: '3.9.5')

        testImplementation       group: 'com.github.kstyrc',       name: 'embedded-redis',           version: '0.6'
        testImplementation scala(group: 'org.scalatestplus',       name: 'mockito-4-5',              version: "${scalaTestVersion}.0")
        testImplementation       group: 'org.apache.kafka',        name: 'kafka-streams',            version: kafkaVersion
        testImplementation       group: 'org.slf4j',               name: 'slf4j-log4j12',            version: '1.7.25'
        testImplementation       group: 'log4j',                   name: 'log4j',                    version: '1.2.17'
        testImplementation       group: 'org.apache.zookeeper',    name: 'zookeeper',                version: '3.6.3'
        testImplementation scala(group: 'io.github.embeddedkafka', name: 'embedded-kafka',           version: kafkaVersion)
        testImplementation scala(group: 'org.scalatest',           name: 'scalatest',                version: scalaTestVersion)
        testRuntimeOnly          group: 'com.vladsch.flexmark',    name: 'flexmark-all',             version: '0.64.0'
    }
    

    test {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    scalafmt {
        configFilePath = "${rootDir}/.scalafmt.conf"
    }

    tasks.check.dependsOn(checkScalafmtAll)
}

publishing {
    publications {
        RedisksPublication(MavenPublication) {
            from components.java
            groupId artifactGroup
            artifactId "redisks_${scalaVersion}"
            version artifactVersion
        }
    }
}

project(':it') {
    dependencies {
        implementation rootProject
        implementation scala(group: 'org.scalatestplus', name: 'mockito-4-5',                   version: "${scalaTestVersion}.0")
        implementation       group: 'com.github.kstyrc', name: 'embedded-redis',                version: '0.6'
        implementation       group: 'org.slf4j',         name: 'slf4j-log4j12',                 version: '1.7.25'
        implementation       group: 'log4j',             name: 'log4j',                         version: '1.2.17'
        implementation       group: 'io.netty',          name: 'netty-transport-native-kqueue', version: '4.1.13.Final'
    }
}

def scala(Object dep) { "${dep.group}:${dep.name}_${scalaVersion}:${dep.version}" }